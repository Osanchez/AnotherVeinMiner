plugins {
    id 'java'
}

// Change the values below this to your mods values
// To change the project name, edit the settings.gradle file

project.ext.modID = "trihardest.anotherveinminer" // The unique id of your mod. Must be all lowercase and cannot use special characters.
project.ext.modName = "AnotherVeinMiner" // The display name of your mod.
project.ext.modVersion = "2.00" // Your current builds version. Must follow the xx.xx... format.
project.ext.gameVersion = "0.21.29" // The target game version.
project.ext.modDescription = "a quality of life mining mod" // Short description of what your mod is.
project.ext.author = "Trihardest" // Your name

def useDecompiledSources = false

/**
 * When setting clientside to true, it means servers do not need this mod for clients to connect and vice versa.
 * IMPORTANT: If you set this to true, make sure that your mod does not add any content or do anything
 * that could cause clients and servers to desync. This includes registering any items, objects, tiles, packets etc.
 */
project.ext.clientside = false

/**
 * The other mod dependencies of this mod
 * Dependencies define the default load order of mods
 * Uncomment and configure these if your mod has dependencies
 */
//project.ext.modDependencies = ["other.modid1", "other.modid2"]
//project.ext.modOptionalDependencies = ["optional.modid1", "optional.modid2"]

// The path to the games install directory
def gameDirectory = "D:/SteamLibrary/steamapps/common/Necesse"

// =================================================
// ========== DO NOT EDIT BELOW THIS LINE ==========
// =================================================

// Name of the jar
def jarName = "${project.ext.modName.replace(" ", "")}-${project.ext.gameVersion}-${project.ext.modVersion}"

group project.ext.modID
version project.ext.modVersion

setSourceCompatibility(JavaVersion.VERSION_1_8)
setTargetCompatibility(JavaVersion.VERSION_1_8)

repositories {
    mavenCentral()
    maven {
        url "https://maven.fabricmc.net/"
    }
}

sourceSets.main.output.resourcesDir = file("build/mod/resources/")
sourceSets.main.java.outputDir = file("build/mod/")
def buildLocation = "build/jar"

compileJava.options.encoding = "UTF-8"

if (!file(gameDirectory + "/Necesse.jar").exists()) {
    throw new Exception("Could not find game install directory. Make sure it is correct in build.gradle file.")
}

def decompileDir = "$gameDirectory/decompiled"
if (project.hasProperty('useDecompiledSources')) {
    useDecompiledSources = project.property('useDecompiledSources') != 'false'
}

configurations {
    runnable { description "For runnable JARs to be ran only on building." }
    runnablePatch { description "Patches/overrides for runnable JARs." }
}

sourceSets {
    runnablePatch {
        java.srcDir "buildscript/src/patches"
        compileClasspath += configurations.runnable
    }
}

dependencies {
    compileOnly files(gameDirectory + "/Necesse.jar")
    compileOnly fileTree(gameDirectory + "/lib/")
    compileOnly fileTree("./mods/") // Add all mods located in local mods folder

    if (useDecompiledSources && file("$decompileDir/Necesse-sources.jar").exists()) {
        compileOnly files("$decompileDir/Necesse-sources.jar")
    } else if (useDecompiledSources) {
        logger.error("useDecompiledSources is set but is not using decompiled sources.")
        logger.error("Run the `decompileToSources` Gradle task to do so. ")
    }
}

classes.doLast {
    // Creates mod.info file
    new File("${sourceSets.main.java.outputDir}/mod.info").text = "{\n" +
            "   id = ${project.ext.modID},\n" +
            "   name = ${project.ext.modName},\n" +
            "   version = ${project.ext.modVersion},\n" +
            "   gameVersion = ${project.ext.gameVersion},\n" +
            "   description = ${project.ext.modDescription},\n" +
            "   author = ${project.ext.author},\n" +
            (project.ext.clientside ? "   clientside = " + project.ext.clientside + ",\n" : "") +
            (project.ext.has("modDependencies") ? "   dependencies = [" + project.ext.modDependencies.join(", ") + "],\n" : "") +
            (project.ext.has("modOptionalDependencies") ? "   optionalDependencies = [" + project.ext.modOptionalDependencies.join(", ") + "],\n" : "") +
            "}"
}

task createAppID {
    group "necesse"
    description "Creates steam_appid.txt file"

    doLast {
        new File("steam_appid.txt").text = "1169040"
    }
}

task decompileToSources(type: JavaExec) {
    dependsOn compileRunnablePatchJava
    onlyIf { useDecompiledSources }
    outputs.upToDateWhen {
        ant.checksum file: "$gameDirectory/Necesse.jar", todir: gameDirectory, fileext: '.sum'
        return file("$gameDirectory/Necesse.jar.sum").exists() &&
                file("$decompileDir/origin.jar.sum").exists() &&
                file("$gameDirectory/Necesse.jar.sum").text == file("$decompileDir/origin.jar.sum").text
    }
    doFirst { mkdir decompileDir }
    doLast {
        ant.move file: "$gameDirectory/Necesse.jar.sum", tofile: "$decompileDir/origin.jar.sum", overwrite: true
        ant.move file: "$decompileDir/Necesse.jar", tofile: "$decompileDir/Necesse-sources.jar", overwrite: true
    }

    classpath sourceSets.runnablePatch.output, configurations.runnable

    def banner = "/*\n * NOTE: Decompiled Necesse.jar sources for modding use only. Redistribution is strictly prohibited.\n */\n\n\n"
    main 'org.jetbrains.java.decompiler.main.decompiler.ConsoleDecompiler'
    args '-hdc=0', '-dgs=1', '-rsy=1', '-rbr=1', '-nls=1', '-mpm=60', '-vac=1', '-bsm=1', '-dol=1', '-iib=1', '-ind=    ', "-ban=$banner", "$gameDirectory/Necesse.jar", decompileDir
}

task runClient(type: JavaExec) {
    group "necesse"
    description "Run client with current mod"
    dependsOn "buildModJar", "createAppID"

    classpath = files(gameDirectory + "/Necesse.jar")

    main = "StartClient"
    jvmArgs "-Xms512m", "-Xmx3G", "-XX:+UnlockExperimentalVMOptions", "-XX:+UseG1GC", "-XX:G1NewSizePercent=20", "-XX:G1ReservePercent=20", "-XX:MaxGCPauseMillis=50", "-XX:G1HeapRegionSize=32M"
    args "-mod \"${buildLocation}\""
}

task runDevClient(type: JavaExec) {
    group "necesse"
    description "Run client with current mod"
    dependsOn "buildModJar", "createAppID"

    classpath = files(gameDirectory + "/Necesse.jar")

    main = "StartClient"
    jvmArgs "-Xms512m", "-Xmx3G", "-XX:+UnlockExperimentalVMOptions", "-XX:+UseG1GC", "-XX:G1NewSizePercent=20", "-XX:G1ReservePercent=20", "-XX:MaxGCPauseMillis=50", "-XX:G1HeapRegionSize=32M"
    args "-dev 1", "-mod \"${buildLocation}\""
}

task runServer(type: JavaExec) {
    group "necesse"
    description "Run server with current mod"
    dependsOn "buildModJar"

    classpath = files(gameDirectory + "/Server.jar")

    main = "StartServer"
    jvmArgs "-Xms512m", "-Xmx3G", "-XX:+UnlockExperimentalVMOptions", "-XX:+UseG1GC", "-XX:G1NewSizePercent=20", "-XX:G1ReservePercent=20", "-XX:MaxGCPauseMillis=50", "-XX:G1HeapRegionSize=32M"
    args "-mod \"${buildLocation}\""
}

task buildModJar(type: Jar) {
    group "necesse"
    description "Generates the mod jar into the build folder"
    dependsOn "classes"
    from {configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }

    // Add mod output
    from sourceSets.main.java.outputDir

    archiveName "${jarName}.jar"
    destinationDir file(buildLocation)
}